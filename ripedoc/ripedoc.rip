begins_with(string, start)
  if start.size > string.size
    return false
  for i in 1:start.size
    if start[i] != string[i]
      return false
  return true

pad(text, width)
  text = text.to_string()
  if text.size < width
    return String.new_uniform(' ', width - text.size) + text
  return text

global doc_group, doc_name, doc_map
do_doc(line)
  arr = line.split_whitespace()
  if begins_with(arr[1], "rdoc")
    # Special line
    if arr[1] == "rdoc-name"
      doc_name = arr[2]
      doc_map[doc_name] = Map.new()
      doc_map[doc_name][&header] = ""
      doc_map[doc_name][&text] = ""
      return
    if arr[1] == "rdoc-header"
      doc_map[doc_name][&header] = String.join(arr[2:], " ")
      return
  else
    doc_map[doc_name][&text] = doc_map[doc_name][&text] + line

global rip_total = 0, rip_empty = 0, rip_comment = 0, rip_doc = 0
process_rip(path)
  f = TextFile.new(path, TextFile.Read)
  for line in f
    line = line.strip()
    if line == ""
      rip_empty = rip_empty + 1
    if begins_with(line, "#")
      rip_comment = rip_comment + 1
    if begins_with(line, "#$")
      rip_doc = rip_doc + 1
      do_doc(line[3:])
    rip_total = rip_total + 1
  f.close()

global c_total = 0, c_empty = 0, c_comment = 0, c_doc = 0
process_c(path)
  f = TextFile.new(path, TextFile.Read)
  for line in f
    line = line.strip()
    if line == ""
      c_empty = c_empty + 1
    if begins_with(line, "//")
      c_comment = c_comment + 1
    if begins_with(line, "//$")
      c_doc = c_doc + 1
      do_doc(line[4:])
    c_total = c_total + 1
  f.close()

process(root)
  for path, type in Path.Walker.new(root)
    name, ext = Path.split_extension(path)
    if type == Path.REGULAR
      if ext == ".rip"
        process_rip(path)
      if ext == ".c" or ext == ".h" or ext == ".y" or ext == ".l"
        process_c(path)

main()
  doc_map = Map.new()

  args = Os.get_args()
  for arg in args
    process(arg)

  Out.println("Line number results:")
  Out.println()
  format = "%a     %a  %a  %a  %a\n"
  Out.printf(format, pad("", 5), "total", "empty", "cmmnt", " doc ")
  Out.printf(format, "C:   ",
                     pad(c_total, 5),
                     pad(c_empty, 5),
                     pad(c_comment, 5),
                     pad(c_doc, 5))
  Out.printf(format, "Ripe:",
                     pad(rip_total, 5),
                     pad(rip_empty, 5),
                     pad(rip_comment, 5),
                     pad(rip_doc, 5))

  tf = TextFile.new("ripedoc.html", TextFile.Write)
  tf.printf("<html>\n  <head><title>Ripe Documentation</title>")
  tf.printf("<link rel=\"stylesheet\" type=\"text/css\" href=\"ripedoc.css\" /></head>\n")
  tf.printf("<body>\n")
  for name, doc in doc_map
    tf.printf("<div class=\"header\">%a</div>", doc[&header])
    tf.printf("<div class=\"text\">%a</div>", doc[&text])
  tf.printf("</body>\n</html>")
