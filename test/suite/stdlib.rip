Map()
  map = { 1 => "one", 2 => "two", 5 => "five" }
  name = "map1"
  Test.test(name, "Map (1 => one, 2 => two, 5 => five)", map.to_string())
  Test.test(name, map[1], "one")
  Test.test(name, map[2], "two")
  Test.test(name, map[5], "five")

  map["England"] = "London"
  map["Serbia"] = "Belgrade"
  map["France"] = "Belgrade"
  map["France"] = "Paris"
  for k, v in map
    Test.test(name, map[k], v)
  Test.test(name, map["France"], "Paris")
  Test.test(name, map.size, 6)
  Test.test(name, true, "Serbia" in map)
  Test.test(name, false, "India" in map)

  map = Map.new()
  for Integer i in 1:100000
    map[i] = i*2
  name = "map2"
  x = 61898
  Test.test(name, map[x], 2*x)
  x = 8592
  Test.test(name, map[x], 2*x)
  x = 100000
  Test.test(name, map[x], 2*x)

Set()
  set = { "Serbia", "France", "England", "Portugal", "Spain" }

  Test.test("Set.size", set.size, 5)
  Test.test("Set.contains?", "Serbia" in set, true)
  Test.test("Set.contains?", "Hungary" in set, false)

  set.remove("Serbia")
  Test.test("Set.remove()", "Serbia" in set, false)
  Test.test("Set.remove()", set.size, 4)

  n = 0
  for v in set
    n = n + 1
    Test.test("Set iterator", v in set, true)
  Test.test("Set iterator", n, 4)

  n = 0
  for i in 1:set.alloc_size
    v = set.get_bucket(i)
    if v != nil
      Test.test("Set.get_bucket", v in set, true)
      n = n + 1
  Test.test("Set.get_bucket", n, 4)

  set.clear()
  Test.test("Set.clear", set.size, 0)
  n = 0
  for v in set
    n = n + 1
  Test.test("Set.clear", n, 0)

arrays()
  name = "arrays"
  my_arr = [1, 2, 3, 4]
  Test.test(name, my_arr.to_string(), "[1, 2, 3, 4]")
  my_arr = []
  Test.test(name, my_arr.to_string(), "[]")

  name = "Array3"
  arr = Array3.new_const(100, 200, 300, 0)
  for x in 1:100
    for y in 1:200
      for z in 1:300
        arr[x, y, z] = x*y*z

  Test.test(name, arr[5, 10, 15], 5*10*15)
  arr = [5, 3, 1, 2, 4]
  arr.sort!()
  Test.test("Array1.sort!()", arr.to_string(),
                              [1, 2, 3, 4, 5].to_string())
  arr = ["serbia", "france", "england", "usa", "italy", "japan"]
  arr.sort!()
  Test.test("Array1.sort!()", arr.to_string(),
     ["england", "france", "italy", "japan", "serbia", "usa"].to_string())

  arr.insert(3, "germany")
  Test.test("Array1.insert()",
            arr.to_string(),
            ["england", "france", "germany", "italy", "japan", "serbia",
             "usa"].to_string())

  view = Array1View.new(arr, 2:4)
  name = "Array1View"
  Test.test(name + " 1", view[1], "france")
  Test.test(name + " -1", view[-1], "italy")
  Test.test(name + " 2", view[2], "germany")
  view[2] = "poland"
  Test.test(name + " index_set", arr[3], "poland")

  view = Array1View.new(arr, 4:2)
  name = "Array1View reverse"
  Test.test(name + " 1", view.map_index(1), 4)
  Test.test(name + " -1", view.map_index(-1), 2)
  Test.test(name + " 2", view.map_index(2), 3)

  name = "Array1.contains?"
  Test.test(name, "serbia" in arr, true)
  Test.test(name, "belgium" in arr, false)

subarrays()
  name = "subarrays"
  my_arr = ["one", "two", "three", "four", "five", "six"]
  test_arr = my_arr[2:4]
  Test.test(name, test_arr.to_string(), "[two, three, four]")
  test_arr = my_arr[:4]
  Test.test(name, test_arr.to_string(), "[one, two, three, four]")
  test_arr = my_arr[5:]
  Test.test(name, test_arr.to_string(), "[five, six]")
  test_arr = my_arr[1:1]
  Test.test(name, test_arr.to_string(), "[one]")
  test_arr = my_arr[6:1]
  Test.test(name, test_arr.to_string(), "[six, five, four, three, two, one]")
  test_arr = my_arr[:]
  Test.test(name, test_arr.to_string(), "[one, two, three, four, five, six]")

TextFile()
  tf = TextFile.new("test/suite/textfile", TextFile.Write)
  tf.printf("line %a\n", 1)
  tf.println("line 2")
  tf.println("line 3")
  tf.close()
  tf = TextFile.new("test/suite/textfile", TextFile.Read)
  name = "TextFile"
  Test.test(name, tf.read_line(), "line 1\n")
  Test.test(name, tf.read_line(), "line 2\n")
  Test.test(name, tf.read_line(), "line 3\n")
  Test.test(name, tf.read_line(), eof)
  tf.close()
  tf = TextFile.new("test/suite/textfile", TextFile.Read)
  Test.test("TextFile.read_entire", tf.read_entire(), "line 1\nline 2\nline 3\n")
  tf.close()

Tuple()
  t = tuple(1, 2, 3)
  Test.test("tuple", t.to_string(), "tuple(1, 2, 3)")
  u = tuple(1, 2, 3)
  v = tuple(1, 2)
  m = Map.new()
  m[t] = 123
  m[v] = 12
  name = "tuple hashing/equality"
  Test.test(name, u in m, true)
  Test.test(name, m[u], 123)
  m[u] = 147
  Test.test(name, m.size, 2)
  Test.test(name, m[t], 147)

Template()
  map = Map.new()
  map[&first_name] = "John"
  map[&last_name] = "Doe"
  Test.test("Template.apply()",
            Template.apply(
              "hello %{ first_name } %{ last_name }", map),
            "hello John Doe")

array_iterators()
  arr = [1, 2, 3, 4, 5]
  arr2 = []
  for element in arr
    arr2.push(element)
  Test.test("Array.get_iter()", arr.to_string(), arr2.to_string())

range_iterators()
  arr = []
  for i in 1:5
    arr.push(i)
  arr2 = []
  for i in 1:
    arr2.push(i)
    if i == 5
      break
  Test.test("Range.get_iter()", arr.to_string(), arr2.to_string())

String()
  s = "  test \t \n"
  Test.test("String.strip()", s.strip(), "test")
  Test.test(
             "String.split()",
             ["red", "riding", "hood"].to_string(),
             "red   riding hood   ".split_whitespace().to_string()
           )
  cat = "cat"
  Test.test("String.index()", cat[2], 'a')
  cat[1] = 'm'
  Test.test("String.index_set()", cat, "mat")

  name = "substring"
  my_string = "123456789"
  Test.test(name, my_string[3:7], "34567")
  Test.test(name, my_string[5:], "56789")
  Test.test(name, my_string[:5], "12345")
  Test.test(name, my_string[:], "123456789")
  Test.test(name, my_string[5:1], "54321")

  cat = "cat"
  dog = "dog"
  Test.test("string +", cat + dog, "catdog")
  Test.test("string +2", 2 + cat, "2cat")

  map = Map.new()
  map["alpha"] = "beta"
  map["c"] = "d"
  Test.test("string.replace", "%{alpha} %{ c }".replace(map), "beta d")

  text = "This is a sentence"
  Test.test("String.find()", text.find("is"), 3)
  Test.test("String.find()", text.find("waldo"), nil)

  text = "alpha"
  text.print("bet")
  Test.test("String.print()", "alphabet", text)

  Test.test("String.substitute()", "bababa".substitute("ba", "lai"), "lailailai")

Std()
  Test.test("Integer()", 123, Integer("123"))
  Test.test("Integer()", 123, Integer(123.0))
  Test.test("Double()", 123.0, Double("123.0"))
  Test.test("Double()", 123.0, Double(123))
  Test.test("Std.printf()", Std.sprintf("format %a %a %%", 1, 2),
                            "format 1 2 %")

Os()
  env = Os.get_environment()
  # Assume at least one element in the environment
  Test.test("Os.get_environment()", true, env.size > 0)
  Test.test("Os.get_env()", nil, Os.get_env("THIS_ENV_VAR_DEFINITELY_DOES_NOT_EXIST"))
  Test.test("Os.get_env()", true, Os.get_env("HOME") != nil)
  Test.test("Os.get_env()", Os.get_env("HOME"), Os.get_env("HOME").to_string())

Time()
  t1 = Time.get_current()
  t2 = Time.get_current()
  Test.test("Time.get_current()", t1 - t2, 0)
  Test.test("Time.get_current()", t1 > 0, true)
  Time.sleep(1)
  t3 = Time.get_current()
  Test.test("Time.sleep()", t3 - t1 >= 1, true)

Num()
  arr = Num.Array1.zeros(6, Num.DOUBLE)
  arr[1] = 1.0
  arr[2] = 2.0
  arr[3] = 3.0
  arr[4] = 4.0
  arr[5] = 5.0
  arr[6] = 6.0

  name = "Num.Array1.rotate"
  arr.rotate(2)
  Test.test(name, arr[5], 1.0)
  Test.test(name, arr[1], 3.0)
  Test.test(name, arr[3], 5.0)
  Test.test(name, arr[2], 4.0)
  arr.rotate(-2)
  Test.test(name, arr[5], 5.0)
  Test.test(name, arr[1], 1.0)
  Test.test(name, arr[3], 3.0)

main()
  Test.set_verbose(false)
  Map()
  Set()
  arrays()
  subarrays()
  TextFile()
  Tuple()
  String()
  array_iterators()
  range_iterators()
  Std()
  Os()
  Time()
  Template()
  Num()

  Test.test("Integer.odd?", 7 .odd?(), true)
  Test.test("Integer.odd?", 8 .odd?(), false)
  Test.test("Integer.even?", 7 .even?(), false)
  Test.test("Integer.even?", 8 .even?(), true)

  # Tally results
  arr = Test.get_results()
  correct = arr[1] - arr[2]
  total = arr[1]
  Err.printf("stdlib test results: %a/%a\n", correct, total)
  if correct < total
    Err.printf("there were %a errors\n", total - correct)
  Test.test("c-expr", $ int64_to_val(1) $, 1)
