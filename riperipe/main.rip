# Copyright (C) 2010  Maksim Sipos <msipos@mailc.net>
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

#######################################################################
# Utilities

sys(args)
  txt = String.join(args, " ")
  if g_verbose
    Err.println(txt)
  if Os.system(args) != 0
    raise "Os.system(\"{}\") failed".f(txt)

load_meta(filename)
  map = Map.new()
  tf = TextFile.new(filename, TextFile.Read)
  for line in tf
    line = line.strip_whitespace()
    n = line.find("=")
    left = line[:(n-1)].strip_whitespace()
    right = line[(n+1):].strip_whitespace()
    map[left] = right
  return map
  tf.close()

# rips, objs, metas = split_files(files)
split_files(files)
  rips = []; objs = []; metas = []; typs = []
  for file in files
    root, ext = Path.split_extension(file)
    switch ext
      case ".rip"
        rips.push(file)
      case ".o"
        objs.push(file)
      case ".meta"
        metas.push(file)
      case ".typ"
        typs.push(file)
      else
        raise "Unknown extension '{}'".f(ext)

  return [rips, objs, metas, typs]

make_asts(rips)
  asts = []
  for rip in rips
    ast = Node.new_from_file(rip)
    asts.push(ast)
  return asts  

stran_asts(asts, rips)
  for i in 1:asts.size
    if not g_omit_typing
      Lang.Stran.absorb_ast(asts[i], rips[i])

process_asts(asts, rips)
  for i in 1:asts.size
    Lang.process(asts[i], rips[i])
    Lang.tree_morph(asts[i])

generate_asts(asts, rips)
  for i in 1:asts.size
    Lang.generate(asts[i], rips[i])  

load_metas(metas)
  for file in metas
    meta = load_meta(file)
    if "cflags" in meta
      g_cflags = g_cflags + " " + meta["cflags"]
    if "lflags" in meta
      g_lflags = g_lflags + " " + meta["lflags"]

absorb_typs(typs)
  for file in typs
    Lang.Stran.absorb_file(file)

#######################################################################
# Globals

var g_verbose = false
var g_cflags = ""
var g_lflags = ""
var g_omit_typing = false
var g_optims = { &FOR_LOOPS   => true,
                 &TYPES       => true,
                 &FUNC_CALLS  => true,
                 &TYPE_VERIFY => false }

is_verbose?()
  return g_verbose

get_optims()
  return g_optims

dump(module_name, out)
  out.println(Wr.dump(module_name))

#######################################################################
# Actions

# asts, rips, objs = go_standard(files)
go_standard(files)
  rips, objs, metas, typs = split_files(files)
  load_metas(metas)
  absorb_typs(typs)
  asts = make_asts(rips)
  return [asts, rips, objs]

go_dump(files)
  asts, rips, objs = go_standard(files)
  stran_asts(asts, rips)
  Lang.genist_run()
  process_asts(asts, rips)
  generate_asts(asts, rips)

  out = StringBuf.new()
  dump("User", out)
  Out.println(out.to_s())

go_bootstrap(files, outfile)
  asts, rips, objs = go_standard(files)
  stran_asts(asts, rips)
  Lang.genist_run()
  process_asts(asts, rips)
  generate_asts(asts, rips)  

  tmp_c_file = Path.temp_name("ripe", ".c")
  tf = TextFile.new(tmp_c_file, TextFile.Write)
  dump("User", tf)
  tf.close()

  tmp_o_file = Path.temp_name("ripe", ".o")
  sys(["gcc", g_cflags, tmp_c_file, "-c", "-o", tmp_o_file])
  Path.remove(tmp_c_file)

  objs_txt = String.join(objs, " ")
  sys(["gcc", g_lflags, objs_txt, tmp_o_file, "-o", outfile])
  Path.remove(tmp_o_file)

go_build(files, outfile)
  asts, rips, objs = go_standard(files)
  stran_asts(asts, rips)
  Lang.genist_run()
  process_asts(asts, rips)
  generate_asts(asts, rips)  

  # Compile ripe
  tmp_c_file = Path.temp_name("ripe", ".c")
  tf = TextFile.new(tmp_c_file, TextFile.Write)
  dump("User", tf)
  tf.close()
  tmp_o_file = Path.temp_name("ripe", ".o")
  sys(["gcc", g_cflags, tmp_c_file, "-c", "-o", tmp_o_file])
  Path.remove(tmp_c_file)

  # Generate module loader, append to other objects
  mods = ["User"]
  mods.extend(Module.get_list())
  loader = Module.make_loader(mods, g_cflags)
  objs.push(loader)
  objs.extend(Module.get_objs())
  objs.push(Path.join(Path.get_program_dir(), "vm.o"))

  objs_txt = String.join(objs, " ")

  cmdline = ["gcc", g_lflags, objs_txt, tmp_o_file, "-o", outfile]
  cmdline.extend(Module.get_lflags())
  sys(cmdline)
  Path.remove(tmp_o_file)
  Path.remove(loader)

go_run(leftover)
  outfile = Path.temp_name("ripe", "")
  go_build(leftover, outfile)
  sys([outfile])
  Path.remove(outfile)

go_module(files, module_name, outfile)
  asts, rips, objs = go_standard(files)
  stran_asts(asts, rips)
  Lang.genist_run()
  process_asts(asts, rips)
  generate_asts(asts, rips)  

  tmp_c_file = Path.temp_name("ripe", ".c")
  tf = TextFile.new(tmp_c_file, TextFile.Write)
  dump(module_name, tf)
  tf.close()

  tmp_o_file = Path.temp_name("ripe", ".o")
  sys(["gcc", "-c", g_cflags, tmp_c_file, "-o", tmp_o_file])
  Path.remove(tmp_c_file)

  objs_txt = String.join(objs, " ")
  sys(["ld", "-r", objs_txt, tmp_o_file, "-o", outfile])
  Path.remove(tmp_o_file)

go_type(files)
  for file in files
    root, ext = Path.split_extension(file)
    if ext == ".rip"
      ast = Node.new_from_file(file)
      Lang.Stran.absorb_ast(ast, file)
    else
      raise "Can only read .rip files in typer mode"
  Lang.Stran.dump()

load_defaults()
  root_dir = Path.get_program_dir()
  meta = load_meta(Path.join(root_dir, "ripe.meta"))
  g_cflags = g_cflags + " " + meta["cflags"]
  g_lflags = g_lflags + " " + meta["lflags"]
  modules = meta["modules"]
  arr = modules.split_whitespace()
  for module in arr
    Module.add(module)
  g_cflags = g_cflags + " -I" + Path.join(root_dir, "include")

main()
  Lang.init()

  options = [
    [&BOOTSTRAP,    "-s", "--bootstrap", 0  , "bootstrap build"],
    [&BUILD,        "-b", "--build", 0, "build program"],
    [&BUILD_MODULE, "-c", "--build-module", 0, "module build"],
    [&DUMP,         "-d", "--dump", 0, "dump C code"],
    [&NAME,         "-n", "--module-name", Opt.ARG, "set module name"],
    [&MODULE,       "-m", "--module", Opt.ARG, "use a module"],
    [&OUTFILE,      "-o", "--outfile", Opt.ARG, "set output filename"],
    [&TYPE,         "-t", "--typer", 0, "create typer output"],
    [&OMIT_TYPING,   nil, "--omit-typing", 0, "omit typing"],
    [&NO_OPTIMS,     nil, "--no-optims", 0, "do not optimize"],
    [&NO_FOR_OPTIMS, nil, "--no-for-optims", 0, "do not optimize for loops"],
    [&NO_FUNC_CALL_OPTIMS, nil, "--no-func-call-optims", 0, "do not optimize function calls"],
    [&OPTIM_VERIFY,  nil, "--optim-verify", 0, "optimize out type verifications"],
    [&CFLAGS,        nil, "--cflags", Opt.ARG, "set flags to C compiler"],
    [&LFLAGS,        nil, "--lflags", Opt.ARG, "set flags to linker"],
    [&VERBOSE,      "-v", "--verbose", 0, "verbose"],
    [&HELP,         "-h", "--help", Opt.HELP, "display help"]
  ]

  args = Os.get_args()
  parsed, leftover = Opt.parse(args, options)

  if leftover.size == 0
    Opt.display_help(options)
    return

  outfile = "r.out"
  module_name = "Module"
  mode = &RUN
  for opt in parsed
    switch opt[1]
      case &BOOTSTRAP
        mode = &BOOTSTRAP
      case &BUILD
        mode = &BUILD
      case &BUILD_MODULE
        mode = &BUILD_MODULE
      case &DUMP
        mode = &DUMP
      case &NAME
        module_name = opt[2]
      case &OUTFILE
        outfile = opt[2]
      case &OMIT_TYPING
        g_omit_typing = true
      case &NO_OPTIMS
        for k, v in g_optims
          g_optims[k] = false
      case &NO_FOR_OPTIMS
        g_optims[&FOR_LOOPS] = false
      case &NO_FUNC_CALL_OPTIMS
        g_optims[&FUNC_CALLS] = false
      case &OPTIM_VERIFY
        g_optims[&TYPE_VERIFY] = true
      case &TYPE
        mode = &TYPE
      case &CFLAGS
        g_cflags = g_cflags + " " + opt[2].strip("\"")
      case &LFLAGS
        g_lflags = g_lflags + " " + opt[2].strip("\"")
      case &MODULE
        Module.add(opt[2])
      case &VERBOSE
        g_verbose = true

  # These modes do not load the defaults
  switch mode
    case &BOOTSTRAP
      go_bootstrap(leftover, outfile)
      return 0
    case &TYPE
      go_type(leftover)
      return 0

  # Remaining modes do load the defaults
  load_defaults()
  switch mode
    case &DUMP
      go_dump(leftover)
    case &BUILD_MODULE
      go_module(leftover, module_name, outfile)
    case &BUILD
      go_build(leftover, outfile)
    case &RUN
      go_run(leftover)
