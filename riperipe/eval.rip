# Copyright (C) 2010  Maksim Sipos <msipos@mailc.net>
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

global unary_ops =  { '-'              => "op_unary_minus",
                      Ast.K_NOT        => "op_unary_not",
                      Ast.K_BIT_NOT    => "op_unary_bit_not" }
global binary_ops = { '+'              => "op_plus",
                      '-'              => "op_minus",
                      '*'              => "op_star",
                      '/'              => "op_slash",
                      '^'              => "op_exp",
                      Ast.K_AND        => "op_and",
                      Ast.K_OR         => "op_or",
                      Ast.K_IN         => "op_in",
                      Ast.K_BIT_AND    => "op_bit_and",
                      Ast.K_BIT_OR     => "op_bit_or",
                      Ast.K_BIT_XOR    => "op_bit_xor",
                      Ast.K_MODULO     => "op_modulo",
                      Ast.OP_EQUAL     => "op_equal",
                      Ast.OP_NOT_EQUAL => "op_not_equal",
                      '>'              => "op_gt",
                      '<'              => "op_lt",
                      Ast.OP_LTE       => "op_lte",
                      Ast.OP_GTE       => "op_gte" }

value_barrier(Tuple t)
  switch t[1]
    case "Value"
      return t[2]
    case "int64"
      return "int64_to_val(%a)".f(t[2])
    case "double"
      return "double_to_val(%a)".f(t[2])
    case "bool"
      return "pack_bool(%a)".f(t[2])
  raise "invalid expression type"

eval_type(node)
  if node.num_children == 1
    return node.get_string("name") + "." + eval_type(node[1])
  else
    return node.get_string("name")

eval_v_expr(node)
  return value_barrier(eval_expr(node))

eval_expr_as_id(Ast.Node expr)
  switch expr.type
    case Ast.ID
      return expr.text
    case Ast.EXPR_FIELD
      parent = expr[1]
      field = expr.get_string("name")
      s = eval_expr_as_id(parent)
      if s != nil
        return "%a.%a".f(s, field)
      else
        return nil
  return nil

eval_expr_list(expr_list, first_comma)
  txt = ""
  index = 1
  for expr in expr_list
    if index == 1
      if first_comma
        txt = ", "
    else
      txt = txt + ", "
    index = index + 1
    txt = txt + eval_v_expr(expr)
  return txt

eval_obj_call(Ast.Node obj, String method_name, args)
  return "method_call%a(%a, %a %a)".f(
                      args.size,
                      eval_v_expr(obj),
                      Dsym.get(method_name),
                      eval_expr_list(args, true) )

eval_index_set(Ast.Node node_self, index_args, Ast.Node assign)
  args = []
  for arg in index_args
    args.push(arg)
  args.push(assign)
  return eval_obj_call(node_self, "index_set", args)

eval_index(Ast.Node node_self, index_args)
  return eval_obj_call(node_self, "index", index_args)

eval_static_call(ssym, arg_list)
  tr = Typer.get_function(ssym)
  #rv_type = tr[1]
  param_types = tr[2]

  num_args = arg_list.num_children
  num_params = param_types.size

  # Check if this static call is to a vararg function.
  is_vararg? = false
  if num_params > 0
    if param_types[-1] == "*"
      is_vararg? = true

  txt = "func_call%a(%a".f(num_params, Ssym.get(ssym))
  # Now generate code.
  if is_vararg?
    min_args = param_types.size - 1
    if num_args < min_args
      raise "'%a' called with %a arguments but expect at least %a".f(
                                                            ssym,
                                                            num_args,
                                                            min_args)
    if min_args > 0
      for index in 1:min_args
        arg = arg_list[index]
        txt = txt + ", %a".f(eval_v_expr(arg))
    txt = txt + ", tuple_to_val(%a".f(num_args - min_args)
    if num_args > min_args
      for index in (min_args+1):num_args
        arg = arg_list[index]
        txt = txt + ", %a".f(eval_v_expr(arg))
    txt = txt + ")"
  else
    if num_args != num_params
      raise "'%a' called with %a arguments but expect %a".f(ssym,
                                                            num_args,
                                                            num_params)
    for arg in arg_list
      txt = txt + ", %a".f(eval_v_expr(arg))
  return txt + ")"

# All expression evaluators that return a Value.
eval_expr_Value(expr)
  switch expr.type
    case Ast.K_NIL
      return "VALUE_NIL"
    case Ast.K_EOF
      return "VALUE_EOF"
    case Ast.ID
      local_var = locals_query(expr.text)
      if local_var == nil
        return Ssym.get(expr.text)
      else
        return local_var.c_name
    case Ast.SYMBOL
      return Dsym.get(expr.text[2:])
    case Ast.STRING
      return "string_to_val(\"%a\")".f(expr.text)
    case Ast.EXPR_ARRAY
      expr_list = expr[1]
      return "array1_to_val2(%a%a)".f(expr_list.num_children,
                                      eval_expr_list(expr_list, true))
    case Ast.EXPR_MAP
      m_list = expr[1]
      Bool is_map = false
      Bool is_set = false
      args = ""

      num_ms = m_list.size
      for m in m_list
        switch m.size
          case 2
            is_map = true
            args = "%a, %a, %a".f(args, eval_v_expr(m[1]), eval_v_expr(m[2]))
          case 1
            is_set = true
            args = "%a, %a".f(args, eval_v_expr(m[1]))

      if is_map and is_set
        raise "invalid curly braces: is this a set or a map?"
      if is_map
        return "ht_new_map(%a%a)".f(num_ms, args)
      if is_set
        return "ht_new_set(%a%a)".f(num_ms, args)
    case Ast.EXPR_INDEX
      return eval_index(expr[1], expr[2])
    case Ast.EXPR_FIELD_CALL
      parent = expr[1]
      field = expr.get_string("name")
      arg_list = expr[2]

      s = eval_expr_as_id(parent)
      if s == nil
        return eval_obj_call(parent, field, arg_list)
      elif locals_query(s) != nil
        return eval_obj_call(parent, field, arg_list)

      return eval_static_call("%a.%a".f(s, field), arg_list)
    case Ast.EXPR_ID_CALL
      left_id = expr[1].text
      arg_list = expr[2]
      if locals_query(left_id) != nil
        raise "variable '%a' called like a function".f(left_id)
      if left_id == "tuple"
        return "tuple_to_val(%a%a)".f(arg_list.num_children,
                                      eval_expr_list(arg_list, true))
      return eval_static_call(left_id, arg_list)
    case Ast.EXPR_RANGE_BOUNDED
      left = expr[1]
      right = expr[2]
      return ("range_to_val(RANGE_BOUNDED, "
              "val_to_int64(%a), val_to_int64(%a))".f(
                                          eval_v_expr(left),
                                          eval_v_expr(right)
                                                     ) )
    case Ast.EXPR_RANGE_BOUNDED_LEFT
      left = expr[1]
      return ("range_to_val(RANGE_BOUNDED_LEFT, "
              "val_to_int64(%a), 0)".f(eval_v_expr(left)))
    case Ast.EXPR_RANGE_BOUNDED_RIGHT
      right = expr[1]
      return ("range_to_val(RANGE_BOUNDED_RIGHT, "
              "0, val_to_int64(%a))".f(eval_v_expr(right)))
    case Ast.EXPR_RANGE_UNBOUNDED
      return "range_to_val(RANGE_UNBOUNDED, 0, 0)"
    case Ast.EXPR_FIELD
      left = expr[1]
      field = expr.get_string("name")
      s = eval_expr_as_id(left)

      # Is it a dynamic field?
      dynamic_field = false
      if s == nil
        dynamic_field = true
      elif locals_query(s) != nil
        dynamic_field = true
      if dynamic_field
        return "field_get(%a, %a)".f(eval_v_expr(left),
                                     Dsym.get(field))

      # For sure it is a static symbol
      return Ssym.get(eval_expr_as_id(expr))

    case Ast.EXPR_AT_VAR
      name = expr.get_string("name")
      if Ctx.class_type_get() != &FIELD
        raise "'@%a' in a class that's not a field class".f(name)
      return "_c_data[%a]".f(Ctx.class_dict_get(name))

    case Ast.C_CODE
      text = expr.text[2:-2]
      text = text.substitute("@", "_c_data->")
      return text

  return ""

eval_expr(expr)
  if expr.size == 1
    if expr.type in unary_ops
      return tuple("Value", "%a(%a)".f(
                                        unary_ops[expr.type],
                                        eval_v_expr(expr[1])
                                      ))
  if expr.size == 2
    if expr.type in binary_ops
      t1 = eval_expr(expr[1])
      t2 = eval_expr(expr[2])

      k1, e1 = t1
      k2, e2 = t2

      v1 = value_barrier(t1)
      v2 = value_barrier(t2)

      return tuple("Value", "%a(%a,%a)".f(binary_ops[expr.type], v1, v2))

  switch expr.type
    case Ast.K_TRUE
      return tuple("bool", "true")
    case Ast.K_FALSE
      return tuple("bool", "false")
    case Ast.INT
      return tuple("int64", expr.text)
    case Ast.DOUBLE
      return tuple("double", expr.text)
    case Ast.CHARACTER
      return tuple("int64", expr.text[2])
    case Ast.EXPR_IS_TYPE
      return tuple("bool", "(obj_klass(%a) == %a)".f(
                                  eval_v_expr(expr[1]),
                                  Type.get(eval_type(expr[2]))
                                      ))

  return tuple("Value", eval_expr_Value(expr))
