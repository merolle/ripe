$
  #include <SDL.h>
  #include <SDL_gfxPrimitives.h>
  #include <SDL_rotozoom.h>
  #include <SDL_ttf.h>
  #include <SDL_image.h>

  #define RED(x)   ((x & 0xFF000000) >> 24)
  #define GREEN(x) ((x & 0x00FF0000) >> 16)
  #define BLUE(x)  ((x & 0x0000FF00) >> 8)
$

namespace Sdl
  namespace Ttf
    init()
      $
        TTF_Init();
      $

    class Font
      $
        TTF_Font *f;
      $

      constructor new(file, ptsize)
        $
          @f = TTF_OpenFont(val_to_string(__file), val_to_int64(__ptsize));
          if (!@f){
            exc_raise("failed to load font '%s'", val_to_string(__file));
          }
        $

      render_blended(text, color)
        surf = Sdl.Surface.new_uninitialized()
        $
          int64 color = val_to_int64(__color);
          SDL_Surface** psurf = obj_c_data(__surf);
          SDL_Color sdl_color = {RED(color), GREEN(color), BLUE(color)};
          *psurf = TTF_RenderText_Blended(@f, val_to_string(__text), sdl_color);
          if (!(*psurf)){
            exc_raise("failed to render string '%s'", val_to_string(__text));
          }
        $
        return surf

  namespace Event
    const QUIT = $ int64_to_val(SDL_QUIT) $

  class Event
    $ SDL_Event event; $

    constructor new()
      pass

    poll()
      return $ pack_bool(SDL_PollEvent(&(@event))) $

    virtual_get type()
      return $ int64_to_val(@event.type) $

  init()
    $
      if (SDL_Init(SDL_INIT_VIDEO) < 0){
        exc_raise("could not initialize SDL: %s", SDL_GetError());
      };
    $

  quit()
    $
      SDL_Quit();
    $

  get_ticks()
    return $ int64_to_val(SDL_GetTicks()) $

  const SWSURFACE = $ int64_to_val(SDL_SWSURFACE) $
  const HWSURFACE = $ int64_to_val(SDL_HWSURFACE) $
  const DOUBLEBUF = $ int64_to_val(SDL_DOUBLEBUF) $
  const FULLSCREEN = $ int64_to_val(SDL_FULLSCREEN) $

  color(r, g, b, a)
    $
      uint32 color = ((val_to_int64(__r) & 0xFF) << 24)
                   | ((val_to_int64(__g) & 0xFF) << 16)
                   | ((val_to_int64(__b) & 0xFF) << 8)
                   | (val_to_int64(__a) & 0xFF);
      return int64_to_val(color);
    $

  class Surface
    $
      SDL_Surface *surf;
    $

    virtual_get width()
      $
        return int64_to_val(@surf->w);
      $

    virtual_get height()
      $
        return int64_to_val(@surf->h);
      $

    constructor new_screen(w, h, flags)
      $
        @surf = SDL_SetVideoMode(val_to_int64(__w),
                                 val_to_int64(__h),
                                 0,
                                 val_to_int64(__flags));
      $

    constructor new_from_file(filename)
      $ const char* filename = val_to_string(__filename);
        @surf = IMG_Load(filename);
        if (!@surf){
          exc_raise("failed to load image from '%s': '%s'", filename,
                                                          IMG_GetError());
        } $

    constructor new(w, h, flags)
      $
        @surf = SDL_CreateRGBSurface(val_to_int64(__flags),
                                     val_to_int64(__w),
                                     val_to_int64(__h),
                                     32,
                                     0xFF000000,
                                     0x00FF0000,
                                     0x0000FF00,
                                     0x000000FF);
      $

    constructor new_uninitialized()
      pass

    put_pixel(x, y, color)
      $
        int64 x = val_to_int64(__x);
        int64 y = val_to_int64(__y);
        if (x < 1 or x > @surf->w or y < 1 or y > @surf->h){
          exc_raise("put_pixel outside surface boundaries (%"PRId64
                    ", %"PRId64")", x, y);
        }
        uint32* p = @surf->pixels;
        p[(x-1) + (y-1) * @surf->w] = val_to_int64(__color);
      $

    filled_circle(x, y, r, color)
      $
        filledCircleColor(@surf, val_to_int64(__x),
                                 val_to_int64(__y),
                                 val_to_int64(__r),
                                 val_to_int64(__color));
      $

    line(x1, y1, x2, y2, color)
      $
        lineColor(@surf, val_to_int64(__x1),
                         val_to_int64(__y1),
                         val_to_int64(__x2),
                         val_to_int64(__y2),
                         val_to_int64(__color));
      $

    rotozoom(angle, scale, smooth)
      $ int smooth = 0; $
      if smooth
        $ smooth = 1; $
      surf2 = Sdl.Surface.new_uninitialized()
      $ SDL_Surface** dsurf = obj_c_data(__surf2);
        *dsurf = rotozoomSurface(@surf, val_to_double(__angle),
                                 val_to_double(__scale), smooth); $
      return surf2

    fill(color)
      $
        Uint32 color = val_to_int64(__color);
        Uint32 surf_color = SDL_MapRGB(@surf->format,
                                       RED(color),
                                       GREEN(color),
                                       BLUE(color));
        SDL_FillRect(@surf, NULL, surf_color);
      $

    blit(surf2, x, y)
      $
        SDL_Surface** dsurf = obj_c_data(__surf2);
        SDL_Rect drect = {val_to_int64(__x), val_to_int64(__y), 0, 0};
        SDL_BlitSurface(@surf, NULL, *dsurf, &drect);
      $

    lock()
      $
        SDL_LockSurface(@surf);
      $

    unlock()
      $
        SDL_UnlockSurface(@surf);
      $

    flip()
      $
        SDL_Flip(@surf);
      $
