$
  #include "modules/Json/json.h"
  #include "modules/Json/json.c"
$

namespace Json
  class Error | parent=Error
    new(text) | constructor
      @text = text
      @value = nil

  parse(s)
    $
      int error = 0;
      char* error_text;
      Value v = json_parse(val_to_string(__s), &error, &error_text);
      if (error){
    $
    raise Json.Error.new("parsing error: '{}'".f($ string_to_val(error_text) $))
    $
      }
    $
    return $v$

  encode(obj)
    out = StringBuf.new()
    Json.encode_to_stringbuf(out, obj)
    return out.to_string()

  encode_to_stringbuf(StringBuf buf, obj)
    if obj is Map
      buf.print("{")
      index = 1
      for k, v in obj
        Json.encode_to_stringbuf(buf, k)
        buf.print(":")
        Json.encode_to_stringbuf(buf, v)
        if index < obj.size
          buf.print(",")
        else
          buf.print("")
        index = index + 1
      buf.print("}")
      return
    if obj is Array1 or obj is Array1View
      buf.print("[")
      index = 1
      for v in obj
        Json.encode_to_stringbuf(buf, v)
        if index < obj.size
          buf.print(",")
        index = index + 1
      buf.print("]")
      return
    if obj is Integer
      buf.print(obj.to_string())
      return
    if obj is Double
      buf.print(obj.to_string())
      return
    if obj is String
      buf.print("\"")
      buf.print(obj)
      buf.print("\"")
      return
    if obj == nil
      buf.print("null")
      return
    if obj == true
      buf.print("true")
      return
    if obj == false
      buf.print("false")
      return
    raise Json.Error.new("Object is of a class that cannot be represented in JSON")
