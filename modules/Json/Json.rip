$
  #include "modules/Json/json.h"
  #include "modules/Json/json.c"
$

namespace Json
  parse(s)
    $
      int error = 0;
      char* error_text;
      Value v = json_parse(val_to_string(__s), &error, &error_text);
      if (error){
    $
    raise "JSON error: " + $ string_to_val(error_text) $
    $
      }
    $
    return $v$

  encode(obj)
    out = StringBuf.new()
    Json.encode_to_stringbuf(out, obj)
    return out.to_string()

  encode_to_stringbuf(StringBuf buf, obj)
    if obj is Map
      buf.write("{\n")
      index = 1
      for k, v in obj
        Json.encode_to_stringbuf(buf, k)
        buf.write(" : ")
        Json.encode_to_stringbuf(buf, v)
        if index < obj.size
          buf.write(",\n")
        else
          buf.write("\n")
        index = index + 1
      buf.write("}")

    if obj is Array1
      buf.write("[")
      index = 1
      for v in obj
        Json.encode_to_stringbuf(buf, v)
        if index < obj.size
          buf.write(",")
        index = index + 1
      buf.write("]")

    if obj is Integer
      buf.write(obj.to_string())
    if obj is Double
      buf.write(obj.to_string())
    if obj is String
      buf.write("\"")
      buf.write(obj)
      buf.write("\"")
    if obj == nil
      buf.write("null")
    if obj == true
      buf.write("true")
    if obj == false
      buf.write("false")
